plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "2.0.1"
    id "net.minecraftforge.gradle.forge" version "d974a94268"
    id "net.kyori.blossom" version "1.2.0"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = "UTF-8"

archivesBaseName = "RewardClaim"
version = "1.1.1"
group = "me.dancedog.rewardclaim"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
}

configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.18.20"
    annotationProcessor "org.projectlombok:lombok:1.18.20"

    include "org.jsoup:jsoup:1.14.1"
    include "com.squareup.okhttp3:okhttp:3.14.9"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

blossom {
    replaceToken("@VERSION@", project.version)
}

shadowJar {
    configurations = [project.configurations.include]

    relocate "org.jsoup", "me.dancedog.rewardclaim.jsoup"
    relocate "okhttp3", "me.dancedog.rewardclaim.okhttp3"
    relocate "okio", "me.dancedog.rewardclaim.okhttp3.okio"

    exclude "META-INF/**/*"
}

reobf {
    shadowJar {}
}

tasks.build.dependsOn reobfShadowJar